apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "mil.nga.geopackage"
archivesBaseName = "geopackage-android"
version = "6.3.1"

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32

        testApplicationId 'mil.nga.geopackage.test'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    configurations {
        javadocDeps
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }
    }

    task javadoc(type: Javadoc) {
        title = "$archivesBaseName $version API"
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.javadocDeps
        options.source = "11"
        options.links "https://ngageoint.github.io/geopackage-core-java/docs/api/"
        options.links "https://ngageoint.github.io/projections-java/docs/api/"
        options.links "https://ngageoint.github.io/coordinate-reference-systems-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-wkb-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-wkt-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-geojson-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-proj-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-java/docs/api/"
        options.links "https://ngageoint.github.io/ogc-api-features-json-java/docs/api/"
        options.links "https://ngageoint.github.io/tiff-java/docs/api/"
        options.links "https://ormlite.com/javadoc/ormlite-core/"
        options.links "https://ormlite.com/javadoc/ormlite-android/"
        options.links "https://hjg.com.ar/pngj/apidocs/"
        options.linksOffline "https://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        destinationDir = file("../javadoc/")
        failOnError false
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.java.srcDirs
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        singleVariant("release")
    }

}

afterEvaluate {

    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.getJavaCompileProvider().get().classpath.files
    })

    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                artifact javadocJar
                artifact sourcesJar

                groupId = group
                artifactId = archivesBaseName
                version = version
                pom {
                    name = 'GeoPackage Android'
                    packaging = 'aar'
                    description = 'GeoPackage Android implementation'
                    url = 'https://github.com/ngageoint/geopackage-android'

                    scm {
                        url = 'git@github.com:ngageoint/geopackage-android.git'
                        connection = 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                        developerConnection = 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                    }

                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://github.com/ngageoint/geopackage-android/blob/master/LICENSE.txt'
                            distribution = 'repo'
                        }
                    }

                    organization {
                        name = 'National Geospatial-Intelligence Agency'
                        url = 'https://www.nga.mil'
                    }

                    developers {
                        developer {
                            id = 'bosborn'
                            name = 'Brian Osborn'
                            email = 'bosborn@caci.com'
                            organizationUrl = 'https://www.caci.com/bit-systems'
                            timezone = 'UTC-07'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                def user = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
                def pw = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
                credentials {
                    username = user
                    password = pw
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'androidx.documentfile:documentfile:1.0.1'
    api 'com.j256.ormlite:ormlite-android:6.1'
    api ('mil.nga.geopackage:geopackage-core:6.3.1'){
        exclude module: 'ormlite-core'
        exclude group: 'com.j256.ormlite'
    }
    api 'ar.com.hjg:pngj:2.1.0'
    api 'mil.nga:tiff:3.0.0'
    api 'mil.nga:sqlite-android:3380400'
    javadocDeps 'com.j256.ormlite:ormlite-android:6.1',
            'mil.nga.geopackage:geopackage-core:6.3.1',
            'ar.com.hjg:pngj:2.1.0',
            'mil.nga:tiff:3.0.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}
